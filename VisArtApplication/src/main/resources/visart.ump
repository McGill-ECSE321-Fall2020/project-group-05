namespace com.ecse321.visart.model;


class Gallery {
  1 gallery <@>- 0..* User;
  1 gallery <@>- 0..* ArtListing artListings;
  // Can there be multiple galleries? We'll need location objects then
  String name;
}

class User {
  1 user -- 1 UserRole role;
  
  String emailAddress;
  unique String username;
  String password;
  unique String idCode;
  unique String displayname;
}

class UserRole {
  
}

class Manager {
  isA UserRole;
  1 manager -- 0..* ArtListing  promotedListings;
}

class Customer {
  isA UserRole;
  1 customer <@>- 0..1 Artist artist;
  1 customer -- 0..* Ticket boughtTickets;
  1 favoritedCustomer -- 0..* ArtListing favoriteListings;
}

class Artist {
  1 artist -- 0..* Ticket soldTickets;
  1 artist -- 0..* ArtListing postedListings;
}

class ArtListing {
  enum PostVisibility {
    Public, Private, Unlisted, Draft
  }
  1 artListing <@>- 0..* ArtPiece pieces;
  1 listing <@>- 0..* Tag tags;
  PostVisibility visibility;
  Float[]  dimensions;
}

class Tag {
  enum TagType {
    Topic, Category, Genre, Material, Other
  }
  TagType type;
  String keyword;
}

class ArtPiece {
  enum PieceLocation {
    AtGallery, Offsite
  }
  PieceLocation basicLocation;
  String addressLocation;
}

class Ticket {
  depend com.ecse321.visart.model.ArtPiece.PieceLocation; 
  1 ticket <@>- 1 ArtOrder order;
  Boolean isPaymentConfirmed; // Artist confirms the payment received
  Double paymentAmount;
}

class ArtOrder {
  depend com.ecse321.visart.model.ArtPiece.PieceLocation; 
  1 artOrder <@>- 1 ArtPiece artPiece;
  Boolean isDelivered;
  PieceLocation targetLocation;
  String targetAddress;
  String deliveryTracker;
}